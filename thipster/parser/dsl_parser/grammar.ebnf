file        = {assignation | resource | outputs}

assignation = VARIABLE, EQ, value

outputs     = OUTPUT, COLON, NEWLINE, list

resource    = type, string, COLON, [if_ctrl] , [nb_ctrl],NEWLINE
                                {parameter, NEWLINE}

parameter   = STRING, COLON, ( value, [if_else_ctrl]
                    | [if_ctrl], NEWLINE (list | dict))

list        = { MINUS, (value, [if_else_ctrl], [nb_ctrl], NEWLINE | dict )}
            | inline_list

inline_list = ( BRACKETS_START [ value { COMMA, value } ] BRACKETS_END )

dict        = { parameter, NEWLINE }

nb_ctrl     = AMOUNT, COLON, arith-expr, [ VARIABLE ]

if_else_ctrl= if_ctrl, [ELSE , value]
if_ctrl     = IF, comp-expr

value       = comp-expr|arith-expr|string|VAR|inline_list|(value if_else_ctrl)

comp-expr   = NOT comp-expr
            | PARENTHESES_START comp-expr (AND|OR) comp-expr PARENTHESES_END
            | arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)
            | BOOL

arith-expr  = term {(PLUS|MINUS) term}

term        = factor {(MUL|DIV|PERCENT) factor}

factor      = (PLUS|MINUS) factor
            | atom {POW factor}

atom        = INT|FLOAT
            | PARENTHESES_START arith-expr PARENTHESES_END

string      = STRING|VAR {INT|STRING|VAR|MINUS|DIV}

type        = STRING {STRING|MINUS|DIV}
